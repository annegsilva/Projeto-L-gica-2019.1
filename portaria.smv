-- Este arquivo descreve o funcionamento de um estacionamento que
-- possui apenas uma via para a entrada e saida de carros, cuja
-- portaria adminitra por meio de dois sinais, o momento em que a
-- passagem está apta para entrada ou para saída. A estrutura da
-- máquina de estados tem como base um semáforo utilizado em sistemas
-- operacionais para a gerencia de mémoria compartilhada.
-- A definicao das variaveis utilizadas na FSM encontra-se a seguir.

-- semaforo1 : flag booleana reprensentando o semaforo que habilita a entrada 
-- semaforo2 : flag booleana reprensentando o semaforo que habilita a saída
---- Verde    = TRUE
---- Vermelho = FALSE

-- c1 : cancela 1
-- c2 : cancela 2
---- aberta  = TRUE
---- fechada = FALSE

-- critico : flag booleana que indica se a região critica (passagen) encontra-se ocupada
---- ocupada = TRUE
---- livre   = FALSE

-- sentido : o sentindo do fluxo da passagem é indicado pelas variaveis booleanas,
--           que são utilizadas no fluxo de controle das cancelas.
---- flag_entrando
---- flag_saindo

-- vagas: indica o número de carros estacionados na garagem

---------------------------------------------------------------
-- link para gráfico da FSM implementada:
-- https://drive.google.com/file/d/1uqUW7nBLYoWa15kP_ZEKHjlgHp_6p8Kj/view?usp=sharing

MODULE main

VAR
  semaforo1 : boolean;
  semaforo2 : boolean;
  passagem : process portaria(semaforo1, semaforo2);

ASSIGN
-- Ambos os semaforos iniciam como VERMELHO (fechados)
  init(semaforo1) := FALSE;
  init(semaforo2) := FALSE;
  
-- Especificações sobre o valor de critico: Quando a região crítica estiver ocupada, a flag critica deve ser verdadeira.
INVARSPEC (passagem.estado = aguardando -> passagem.critico = FALSE);
INVARSPEC (passagem.estado = entrando -> passagem.critico = TRUE);
INVARSPEC (passagem.estado = saindo -> passagem.critico = TRUE);
-- Especificações sobre os valores dos semáforos: Quando ocupado, os semáforos devem ser complementares
INVARSPEC (semaforo1 = TRUE -> semaforo2 = FALSE);
INVARSPEC (semaforo2 = TRUE -> semaforo1 = FALSE);
-- Especificações sobre as flags de fluxo: Quando ocupado, as flags devem ser complementares
INVARSPEC (passagem.flag_entrando = TRUE -> passagem.flag_saindo = FALSE)
INVARSPEC (passagem.flag_saindo = TRUE -> passagem.flag_entrando = FALSE)
-- Especificações sobre o comportamento das cancelas: Cancelas nunca abrem simultaneamente
INVARSPEC (passagem.estado = c1_aberta -> passagem.c2 = FALSE)
INVARSPEC (passagem.estado = c2_aberta -> passagem.c1 = FALSE)
-- Especificação sobre os valores limites para vagas: Nunca menor que zero e maior que cem.
INVARSPEC (passagem.vagas >= 0 & passagem.vagas <= 100)
---------------------------------------------------------------

MODULE portaria(semaforo1, semaforo2)
VAR
  estado : {aguardando, ocupado, entrando, saindo, c1_aberta, c1_fechada, c2_aberta, c2_fechada, livre};
  c1: boolean;
  c2: boolean;
  critico: boolean;
  flag_entrando: boolean;
  flag_saindo: boolean;
  vagas: 0 .. 100;

ASSIGN
  init(c1) := FALSE;
  init(c2) := FALSE;
  init(critico) := FALSE;
  init(flag_entrando) := FALSE;
  init(flag_saindo) := FALSE;
  init(vagas) := 0;

  init(estado) := aguardando;
  next(estado) := 
  case 
    -- Muda de estado de forma nao deterministica
    estado = aguardando : {aguardando, ocupado};
    estado = ocupado : {entrando, saindo};
    
    -- Fluxo de entrada
    (estado = entrando) & (semaforo1=TRUE):c1_aberta;
    (estado = c1_aberta) & (c1=FALSE): c1_fechada;
    (estado = c1_fechada) & (c2=TRUE): c2_aberta;
    (estado = c2_aberta) & (c2=FALSE): c2_fechada;
    (estado = c2_fechada) & (flag_entrando=TRUE): livre;

    -- Fluxo de saida
    (estado = saindo) & (semaforo2=TRUE) & (vagas >= 1): c2_aberta;
    (estado = c2_aberta) & (c2=FALSE):  c2_fechada;
    (estado = c2_fechada) & (c1=TRUE):  c1_aberta;
    (estado = c1_aberta) & (c1=FALSE):  c1_fechada;
    (estado = c1_fechada) & (flag_saindo=TRUE): livre;
    
    -- Carro entrou ou saiu com sucesso
    (estado = livre): aguardando;

    TRUE : estado;
    esac;
  
  next(semaforo1) := 
    case
      (estado = entrando) & (vagas < 100) : TRUE;
      estado = saindo : FALSE;
      estado = aguardando: FALSE;
      TRUE : semaforo1;
    esac;

  next(semaforo2) := 
    case
      estado = entrando : FALSE;
      (estado = saindo) & (vagas >= 1): TRUE;
      estado = aguardando: FALSE;
      TRUE : semaforo2;
    esac;

  next(c1) :=
    case
      estado = entrando: TRUE;
      estado = c1_aberta: FALSE;
      estado = c2_fechada: TRUE;
      estado = aguardando: FALSE;
      TRUE: c1;
    esac;

  next(c2) :=
    case
      estado = saindo: TRUE;
      estado = c1_fechada: TRUE;
      estado = c2_aberta: FALSE;
      estado = aguardando: FALSE;
      TRUE: c2;
    esac;

  next(critico) :=
    case
      estado = ocupado: TRUE;
      estado = livre: FALSE;
      TRUE : critico;
    esac;

  next(flag_entrando) :=
    case
      estado = entrando: TRUE;
      estado = saindo: FALSE;
      estado = aguardando: FALSE;
      TRUE: flag_entrando;
    esac;

  next(flag_saindo) :=
    case
      estado = entrando: FALSE;
      estado = saindo: TRUE;
      estado = aguardando: FALSE;
      TRUE: flag_saindo;
    esac;

  next(vagas) :=
  case
    (estado = livre) & (flag_entrando=TRUE) & (vagas < 100) : vagas + 1;
    (estado = livre) & (flag_saindo=TRUE) & (vagas >= 1): vagas - 1;
    TRUE: vagas;
  esac;

FAIRNESS
  running